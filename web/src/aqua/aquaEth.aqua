data Result:
    success: bool
    reason: string
    code: u32
    message: string

data ResultString:
    info: Result
    data: string

-- Same as result string but the user knows to parse JSON string
data ResultJSONString:
    info: Result
    data: string

func identityResultString(val: ResultString) -> ResultString:
    <- val 

data ResultAddresses:
    info: Result
    data: []string

data JSONPacket:
    type: string
    data: string

data Currency:
    name: string
    symbol: string
    decimals: u32 

data Chain:
    name: string
    chainId: u32
    shortName: string
    network: string
    networkId: u32
    currency: Currency

data ResultChain
    info: Result
    data: Chain

service Ethereum("ethereum"):
    -- Methods that execute on the remote client (The one with MetaMask)
    requestAccounts() -> ResultAddresses
    getChainInfo() -> ResultChain
    getBalance(address: string) -> ResultString
    getBlockNumber() -> ResultString

    -- Wei to Eth
    formatEther(amount: string) -> ResultString
    -- Eth to Wei
    parseEther(amount: string) -> ResultString
    
    -- Methods to register a listener for window.ethereum events on the
    -- local node
    registerListenerNode(listenerPeerId: string, listenerRelayId: string)
    receiveData(packet: JSONPacket)

func requestAccounts(peerId: string, relayId: string) -> ResultAddresses:
    on peerId via relayId:
        res <- Ethereum.requestAccounts()
    <- res
    
func getChainInfo(peerId: string, relayId: string) -> ResultJSONString:
    on peerId via relayId:
        res <- Ethereum.getChainInfo()
    <- res

func registerListenerNode(peerId: string, relayId: string, listenerPeerId: string, listenerRelayId: string):
    on peerId via relayId:
        Ethereum.registerListenerNode(listenerPeerId, listenerRelayId)
    
func listenerNodeCallback(peerId: string, relayId: string, jsonPacket: JSONPacket):
    on peerId via relayId:
        Ethereum.receiveData(jsonPacket)

func getBalance(peerId: string, relayId: string, address: string) -> ResultString:
    resBox: *ResultString

    on peerId via relayId:
        blockRes <- Ethereum.getBalance(address)
        if blockRes.info.success:
            resBox <- Ethereum.formatEther(blockRes.data)
        else:
            resBox <- identityResultString(blockRes)
            
    <- resBox! 

func getBlockNumber(peerId: string, relayId: string) -> ResultString:
    on peerId via relayId:
        res <- Ethereum.getBlockNumber()
    <- res

func formatEther(peerId: string, relayId: string, amount: string) -> ResultString:
    on peerId via relayId:
        res <- Ethereum.formatEther(amount)
    <- res

func parseEther(peerId: string, relayId: string, amount: string) -> ResultString:
    on peerId via relayId:
        res <- Ethereum.parseEther(amount)
    <- res